#!/usr/bin/perl -wT
use strict;
use lib 'lib';
use Getopt::Long;
use QVD::CommandInterpreter;
use IO::Socket::INET;
use Log::Log4perl qw(:easy);
use POSIX ":sys_wait_h";
$| = 1;

# Protocol:
#
# The prompt is "\n> "
# 
# The format is a command/response pattern, except for commands that end
# the session. Currently this is the "socat" command when it succeeds.
#
# Responses are terminated by a blank line.
# Errors start with "ERROR:"
# Fatal errors start with "FATAL:"
#
# Sample session:
#
#########################
# > help
# Commands:
#     help
#     version
#     socat
#
# > version
# 3.1.123
#
# > socat /dev/ttyS0
#########################
#
#

my $config_file = "/etc/qvd/qvdcmd.conf";
my $debug;
my $help;
my %args;
my $listen;

my $log_config = '/etc/qvd/qvdcmd-log.conf';
if ( -f $log_config ) {
	Log::Log4perl::init($log_config);
} else {
	Log::Log4perl::easy_init($ERROR);
}

my $log = Log::Log4perl->get_logger('qvdcmd');
$SIG{__WARN__} = sub { $log->warn(shift); };
$SIG{__DIE__}  = sub { $log->error(shift); };

delete $ENV{PATH};

GetOptions(
	"config|c=s"    => \$config_file,
	"debug|D"       => \$debug,
	"help|h"        => \$help,
        "listen|l=i"    => \$listen
) or die "Bad arguments";

if ( $help ) {
	print <<HELP;
Syntax: $0 [options]

Options:
	--help, -h	Show this help
	--config, -c	Specify the config file to use

HELP
	exit(0);
}


$args{options} = {
	debug => $debug
};
use File::Temp qw(tempdir);
use Proc::Background;


# Untaint the config file. The taint check is for the data coming
# from the socket.

$config_file = untaint($config_file);

if ( $config_file && -f $config_file ) {
	$log->info("Loading config file $config_file");

	eval {
		$args{config} = do($config_file);
	};
	if ($@) {
		print "FATAL: Syntax error in config file: $@\n";
		exit(1);
	}

	if (!$args{config} || ref($args{config}) ne "HASH" || !$args{config}->{socat}) {
		print "FATAL: Bad config file\n";
		exit(2);
	}

}

if ( $listen ) {
	my $terminate = 0;
	my $term_sig  = "";

	$log->info("Listening on localhost, port $listen");

	my $listener = new IO::Socket::INET(Listen    => 5,
	                                    LocalAddr => 'localhost',
	                                    LocalPort => $listen);

	if ( !$listener ) {
		die "Failed to listen on port $listen";
	}

	#$SIG{CHLD} = sub { while( waitpid( -1, &WNOHANG ) > 0 ) {} };
	$SIG{HUP}  = 'IGNORE';
	$SIG{TERM} = sub { $terminate = 1; $term_sig = "SIGTERM" };
	$SIG{INT}  = sub { $terminate = 1; $term_sig = "SIGINT" };

	while((!$terminate) && (my $socket = $listener->accept())) {
		my $fileno = fileno($socket);

		$log->info("Accepted connection");

		if ( defined $fileno ) {
			my $pid = fork();

			if ( !$pid ) {
				#$SIG{CHLD} = 'DEFAULT';
				$SIG{TERM} = 'DEFAULT';
				$SIG{INT}  = 'DEFAULT';

				open(STDIN, "<&$fileno") or die "Can't reopen stdin: $!";
				open(STDOUT, ">&$fileno") or die "Can't reopen stdout: $!";
				STDIN->autoflush(1);
				STDOUT->autoflush(1);
				print "Connected\n";
				my $cmd = new QVD::CommandInterpreter( %args );
				$cmd->run();
				$socket->shutdown(2);

				$log->debug("Child process $$ exiting");
				exit(0);
			} else {
				$log->debug("Started child $pid");
			}
		} else {
			die "no file handle!";
		}
	}
	
	if ( $terminate ) {
		$log->info("Terminating due to signal $term_sig");
	} else {
		$log->info("Listener terminated, odd");
	}
	$listener->shutdown(2);
	$listener->close();

	
} else {
	my $cmd = new QVD::CommandInterpreter( %args );
	$cmd->run();
}

sub untaint {
	my $arg = shift;
	$arg =~ /(.*)/;
	return $1;
}

